! There are flags at morpheme boundaries.
set flag-is-epsilon ON

define Sigma [ a | b | c | d | e | g | h | i | k | m | n | o | s | t | w | y | z ] ;
define Cons [ b | c | d | g | h | k | m | n | s | t | w | y | z ] ;
define Vowel [ a | e | i | o ] ;

! Sometimes there may be more than one consecutive
! morpheme boundary. Reduce those to 1.
define PrefixBoundaryCleanup ["<<"]+ @-> "<<" ;
define SuffixBoundaryCleanup [">>"]+ @-> ">>" ;


! d-deletion: Delete stem-final "d" when the suffix complex starts with a consonant.
define dDeletion d -> 0 || _ ">>" Cons ;

! This rule MUST PRECEED the "ShortV-deletion" rule
! i1-Palatalization: Stems ending in "n" palatalize to "zh" and "s" to "sh" when the suffix complex starts with the first person theme sign "i1".
define n2zhRule n -> {zh} || _ ">>" i1 ;
define s2shRule s -> {sh} || _ ">>" i1 ;

define i1Rule i1 -> i ;

! Nasal assimilation: With stems ending in "m" (e.g. VAI stems "minongwaam"), the "m" changes to "n" when the suffix complex starts with "z" (negation) or a
! "g" (3SgProx in the conjunct) or "d" (inclusive simple imperative)
define amRule1 m -> a || a _ ">>" [m|n] ;
define amRule2 m -> n || a _ ">>" [z|g|d] ;

! aw-to-aa: For stems that end in "aw", the "aw" goes to "aa" when the suffix complex starts with "g" or "k".
! aw-to-oo: For stems that end in "aw", the "aw" goes to "oo" when the suffix complex starts with "n" or "s".
define awaaRule w -> a || a _ ">>" [k|g] ;
define awooRule a w -> o o || _ ">>" [n|s] ;

!define nDeletion n -> 0 || [ i | o o ] _ ">>" Cons ;

! ShortV-deletion: Delete word-final short vowels
! This rule MUST PRECEED the "w-deletion" rule
define vowelDeletion [ Vowel - e ] -> 0 || Cons (">>") _ (">>") .#. ;

! w-to-o: For stems that end in "Cw", the "w" goes to "o" when the suffix complex starts with an "i" or "i1". AKA, wi -> o / C __
define wRule [ w -> o1 || Cons _ ">>" [i|i1] ] .o. [[i|i1] -> 0 || o1 ">>" _ ] .o. [o1 -> o];

! W-deletion: Delete word-final "w" (occurs with neutral, positive 3sg)
! This role MUST FOLLOW i1-Palatalization and w-to-o
define wDeletion w -> 0 || _ (">>") .#. ;

! Sources:
! https://ojibwe.lib.umn.edu/main-entry/g-pf
! https://ojibwe.lib.umn.edu/main-entry/g-pf
! https://ojibwegrammar.langsci.wisc.edu/Assets/Pdfs/InflAnishPersonPrefixes1.02.pdf
define PrefixDInsertion [..] -> d || [ [ n | g ] i | o ] _ "<<" Vowel ;
define PrefixOLengthening o -> {oo} || [ [ n | g ] i | o ] d "<<" _ \o ; 

define cleanUp ["<<"|">>"] -> 0 ; 

read lexc VAIO_CNJ.lexc
define VAIOCNJ ;

read lexc VAIO_IMP.lexc
define VAIOIMP ;

read lexc VAIO_IND.lexc
define VAIOIND ;

read lexc VAI_CNJ.lexc
define VAICNJ ;

read lexc VAI_IMP.lexc
define VAIIMP ;

read lexc VAI_IND.lexc
define VAIIND ;

read lexc VAI_Reflex_Recip.lexc
define VAIReflexRecip ;

read lexc VII_CNJ.lexc
define VIICNJ ;

read lexc VII_IND.lexc
define VIIIND ;

read lexc VTA_CNJ.lexc
define VTACNJ ;

read lexc VTA_IMP.lexc
define VTAIMP ;

read lexc VTA_IND.lexc
define VTAIND ;

read lexc VTA_IRR.lexc
define VTAIRR ;

read lexc VTI_CNJ.lexc
define VTICNJ ;

read lexc VTI_IMP.lexc
define VTIIMP ;

read lexc VTI_IND.lexc
define VTIIND ;

!read lexc lexc_source/verb_prefixes.lexc
!define VPrefixes ;
!read regex "<<" VPrefixes ;
!define Prefixes ;

regex [ VAIOCNJ |
      	VAIOIMP |
	VAIOIND |
	VAICNJ |
	VAIIMP |
	VAIIND |
	VAIReflexRecip |
	VIICNJ |
	VIIIND |
	VTACNJ |
	VTAIMP |
	VTAIND |
	VTICNJ |
	VTIIMP |
	VTIIND ] ;

! FIXME: We need an "insert preverbs here" flag in the lexicon.
! We don't want to substitute "<<" because that causes problems
! later.
substitute defined Prefixes for "<<"
define Lexicon ;

read regex cleanUp.i .o. Lexicon .o. [ dDeletion .o.
     	   	     	 	       n2zhRule .o.
				       s2shRule .o.
     	   	                       amRule1 .o.
     	   	                       amRule2 .o.
				       awaaRule .o.
				       awooRule .o.
				       wRule .o.
				       i1Rule .o.
			               vowelDeletion .o.
			               wDeletion .o.				      
!			               nDeletion .o.
			               PrefixDInsertion .o.
			               PrefixOLengthening .o.
			               cleanUp ] ;

define Analyzer ;

regex [ VTAIRR ] ;
define IrregularLexicon ;

read regex Analyzer | IrregularLexicon ;

!define Analyzer

! Closed classes
!read lexc lexc_source/numerals.lexc
!define Numerals ;

!read lexc lexc_source/particles.lexc
!define Particles ;

!read lexc lexc_source/adverbs.lexc
!define Adverbs ;

!read lexc lexc_source/quantifiers.lexc
!define Quantifiers ;

!read lexc lexc_source/pronouns.lexc
!define Pronouns ;

!read regex [ Analyzer |
!             Numerals |
!	     Particles |
!	     Adverbs |
!	     Quantifiers |
!	     Pronouns ];

save stack ojibwe.fomabin